apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "maven-publish"

apply from: "../dependencies.gradle"

android {
  compileSdkVersion versions.compileSdk
  buildToolsVersion versions.buildTools

  compileOptions {
    sourceCompatibility 1.8
    targetCompatibility 1.8
  }

  defaultConfig {
    minSdkVersion versions.minSdk
    targetSdkVersion versions.compileSdk
    versionCode versions.publishVersionCode
    versionName versions.publishVersion
  }

  sourceSets {
    main.res.srcDirs = [
        "src/main/res",
        "src/main/res-public"
    ]
  }
}

afterEvaluate {
  publishing {
    repositories {
      maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/msfjarvis/github-packages-deployment-sample")
        credentials {
          username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
          password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
        }
      }
    }
    publications {
      release(MavenPublication) {
        from components.release
        groupId = "$GROUP"
        artifactId = "deployment-sample-library"
        // Use your configured version outside CI, the SHA of the top commit inside.
        version = System.env['GITHUB_SHA'] == null ? "$VERSION" : System.env['GITHUB_SHA']
      }
    }
  }
}

dependencies {
  api deps.support.app_compat
  implementation deps.kotlin.stdlib8

  testImplementation deps.kotlin.test.mockito
  testImplementation deps.test.robolectric
  testImplementation deps.test.junit
  testImplementation deps.test.mockito_core
  testImplementation deps.test.truth
}

apply from: "../spotless.gradle"
